---
title: 编译器编写工具
---

* lex/yacc程序组成结构、文件格式。
* 如何在lex/yacc中使用C++和STL库，用extern "C"声明那些lex/yacc生成的、要链接的C函数，如yylex(), yywrap(), yyerror()。
* 重定义YYSTYPE/yylval为复杂类型。
* lex里多状态的定义和使用，用BEGIN宏在初始态和其它状态间切换。
* lex里正则表达式的定义、识别方式。
* lex里用yylval向yacc返回数据。
* yacc里用%token<>方式声明yacc记号。
* yacc里用%type<>方式声明非终结符的类型。
* 在yacc嵌入的C代码动作里，对记号属性($1, $2等)、和非终结符属性($$)的正确引用方法。

# LEX

  Lex的基本工作原理为：由正规式生成NFA，将NFA变换成DFA，DFA经化简后，模拟生成词法分析器。

  其中正规式由开发者使用Lex语言编写，其余部分由Lex翻译器完成.翻译器将Lex源程序翻译成一个名为lex.yy.c的C语言源文件，此文件含有两部分内容：一部分是根据正规式所构造的DFA状态转移表，另一部分是用来驱动该表的总控程序yylex()。当主程序需要从输入字符流中识别一个记号时，只需要调用一次yylex()就可以了。



```C
%{
#include <stdio.h>
#include "y.tab.h"
void yyerror(char *);
%}
NUM [1-9]+[0-9]*|0
%%

{NUM}		                sscanf(yytext,"%d",&yylval.inum); return NUM;
"+"							return ADD;
"-"							return SUB;
"*"							return MUL;
"/"							return DIV;
\n                          return CR;
[ \t]+                     /* ignore whitespace */;
.
%%

```

## 第一部分

>  正规定义

\#include “y.tab.h”

* bison在翻译代码时自动生成的头文件（这也是为什么我们先翻译.y文件），如果不加上这个头文件，则编译会出错。

## 第二部分

> 词法规则

* **Lex的匹配策略**
  * 按最长匹配原则确定被选中的单词。
  * 如果一个字符串能被若干正规式匹配，则先匹配排在前面的正规式。

* return 的token种别：
  * 这些符号的定义就在t.tab.h头文件中，它们都是一些互不相同的常量，这也是为什么不使用该头文件就会报错。我们会在编写bison代码时手动定义这些符号。



flex在翻译为C文件时会产生一些自定义的函数和全局变量

![在这里插入图片描述](https://img-blog.csdnimg.cn/20200919193117969.png?x-oss-process=image/watermark,type_ZmFuZ3poZW5naGVpdGk,shadow_10,text_aHR0cHM6Ly9ibG9nLmNzZG4ubmV0L3dlaXhpbl80NDAwNzYzMg==,size_16,color_FFFFFF,t_70#pic_center)

* ECHO：Lex中预定义的宏，可以出现在动作中，相当于fprintf(yyout, “%s”,yytext)
*  yywrap()：词法分析器遇到文件结尾时会调用yywrap()来决定下一步怎么做：
  * 若yywrap()返回0，则继续扫描
  * 若返回1，则返回报告文件结尾的0标记。
  * 由于词法分析器总会调用yywrap，因此辅助函数中最好提供yywrap，如果不提供，则在用C编译器编译lex.yy.c时，需要链接相应的库，库中会给出标准的yywrap函数（标准函数返回1）



## 第三部分

> 辅助函数

* 一般来说，语法分析器每次都调用一次    yylex()获取一个记号。如果想自己写一个程序使用lex生成的词法分析器，则只需要在自己的程序中按需要调用yylex()函数即可。
  * 注意：yylex()调用结束后，输入缓冲区并不会被重置，而是仍然停留在刚才读到的地方。并且，词法分析器当前所处的状态（%s定义的那些状态）也不会改变。

# YACC

## 概念

Yacc 全称 Yet Another Compiler Compiler

* Yacc是一个用来生成编译器的编译器（编译器代码生成器）。yacc生成的编译器主要是用C语言写成的语法解析器（Parser），需要与词法解析器Lex一起使用，再把两部份产生出来的C程序一并编译。
* 作为 Yacc 对说明文件中的 %token NUMBER 声明的对应。Yacc 坚持定义所有的符号记号本身，而不是从别的地方引入一个定义。但是却有可能通过在记号声明中的记号名之后书写一个值来指定将赋给记号的数字值。
* Yacc的输入是巴科斯范式（BNF）表达的语法规则以及语法规约的处理代码，Yacc输出的是基于表驱动的编译器，包含输入的语法规约的处理代码部分。
* Yacc是开发编译器的一个有用的工具,采用LR（1）/LALR(1)语法分析方法。
* 这种方法具有分析速度快，能准确，即使地指出出错的位置，它的主要缺点是对于一个使用语言文法的分析器的构造工作量相当大，k愈大构造愈复杂，实现比较困难。



一个LR分析器有3个部分组成：

* 总控程序，也可以称为驱动程序。
  对所有的LR分析器总控程序都是相同的。
* 分析表或分析函数。
  不同的文法分析表将不同，同一个文法采用的LR分析器不同时，分析表也不同，分析表又可分为动作(ACTION)表和状态转换(GOTO)表两个部分，它们都可用二维数组表示。
* 分析栈，包括文法符号栈和相应的状态栈。
  它们均是先进后出栈。 分析器的动作由栈顶状态和当前输入符号所决定(LR(0)分析器不需要向前查看输入符号)。
* LR分析器工作过程如下 :
  其中SP为栈指针，S[i]为状态栈，X[i]为文法符号栈。状态转换表内容按关系GOTO[Si，X] = Sj确定，该关系式是指当栈顶状态为Si遇到当前文法符号为X时应转向状态Sj。X为终结符或非终结符。 ACTION[Si，a]规定了栈顶状态为Si是遇到输入符号a应执行的动作。


动作有4种可能：

* 移进：
  当Sj = GOTO[Si，a]成立，则把Sj移入到状态栈，把a移入到文法符号栈。其中i，j表示状态号。
* 规约：
  当在栈顶形成句柄为β时，则用β归约为相应的非终结符A，即当文法中有 A-->β的产生式，而β的长度为r（即|β| = r）,则从状态栈和文法符号栈中自栈顶向下去掉r个符号，即栈指针SP减去r。并把A移入文法符号栈内，再把满足Sj = GOTO[Si，A]的状态移进状态栈，其中Si为修改指针后的栈顶状态。
* 接受acc:
  当规约到文法符号栈只剩文法的开始符号S时，并且输入符号串已结束即当前输入符是‘#’，则为分析成功。
* 报错：
  当遇到状态栈顶为某一状态下出现不该遇到的文法符号时，则报错，说明输入串不是该文法能接受的句子。



```C
%{
#include <stdio.h>
#include <string.h>
int yylex(void);
void yyerror(char *);
%}

%union{
  int inum;
  double dnum;
}
%token ADD SUB MUL DIV CR
%token <inum> NUM
%type  <inum> expression term single

%%
       line_list: line
                | line_list line
                ;
				
	       line : expression CR  {printf(">>%d\n",$1);}

      expression: term 
                | expression ADD term   {$$=$1+$3;}
				| expression SUB term   {$$=$1-$3;}
                ;

            term: single
				| term MUL single		{$$=$1*$3;}
				| term DIV single		{$$=$1/$3;}
				;
				
		  single: NUM
				;
%%
void yyerror(char *str){
    fprintf(stderr,"error:%s\n",str);
}

int yywrap(){
    return 1;
}
int main()
{
    yyparse();
}
```

bison程序维护了两个栈

* 一个是**文法符号栈**，用来进行语法上的归约和移进；

* 另一个则是**属性值栈**，这个栈内的值是与文法符号栈一一对应的，当一个文法符号被压栈时，它的值也被压进了属性值栈。因此，我们可以在属性值栈中寻找我们需要的值。

  * bison的全局变量yylval。在flex进行词法分析时，一旦分析成功，我们就在action中令yylval等于需要传入的值。

  * bison中还有一个宏，叫做YYSTYPE，表示yylval的数据类型

  * 在**bison程序的第一部分**加入如下定义：

    ```c
    %union{
      int inum;
      double fnum;
      char c;
      char * string;
      //其余类型随意加
    }
    ```

## 第一部分

* %token后面，跟着一些字符串，我们就是在这里定义了这些符号，它们会被翻译成C头文件，被flex引用，然后又通过yylex()函数return回来。

  * ```
    lex使用该头文件中的标志定义。Yacc调用lex的yylex()来获得标志（token），与标志对应的值由lex放在变量yylval中。yylval的类型由YYSTYPE决定，YYSTYPE缺省类型是int。如：
    [0-9]+ {
    yylval = atoi(yytext);
    return INTEGER;
    }
    
    ```

* %token的另一种用法：将联合体的一个变量名用尖括号括起，后跟终结符（可以为多个），就能表示它们的值使用的是这个变量

* %type：也是指示类型，但这次指示的是非终结符。

void yyerror(char *);

* bison代码中要用到的报错函数，既然是bison的报错函数，那么为什么不在bison中定义呢？事实上我们在两个代码中都定义了这个函数，这是因为程序不仅在语法分析阶段会报错，词法分析阶段也是会报错的。其实bison会自动定义这个函数，但我们想要使用自己编写的报错函数的话，则要重新声明它，否则会报警告。（我们希望尽量不被警告）

## 第二部分

* 语法的产生式/规约
* expression SUB term   {$$=$1-$3;}
  * 在这个规则中，$1表示的是expression的值，$3表示的是term的值，而两者相减后将值传给$$，这就是归约后的符号的值。此外，由于并没有定义过SUB的类型，不存在它的记号值，所以使用$2的话会报错。
  * 专业解释：在语义动作中，可以引用存放在属性值栈中的文法符号的属性值，$$符号可引用产生式左部非终结符的属性值，而$i则可以引用产生式右部第i个文法符号的属性值，当然，前提是你必须为这些文法符号的属性定义了类型，而且在你使用这些属性值之前，已经将这些属性值放入了属性值栈对应的地方。



## 第三部分

yyerror具体的报错操作；

yywrap函数同之前在flex的讲述；

而yyparse函数就是bison的语法分析函数



## 高级YACC

* Q1：如果语法规则中，出现了移进-归约冲突和归约-归约冲突，bison:会怎么做？
  * A1：当存在冲突时，bison将执行默认动作。当存在移进-归约冲突时，bison将进行移进。当存在归约-归约冲突时，bison将执行列出的第一条规则。对于任何冲突，它都会显示警告信息。所以应该书写明确的语法规则，这通常需要我们掌握一些消除模糊性的方法。
* Q2：bison的第一部分，还有什么用以定义的符号吗？
  * A2：包括但不限于%left，%right，%nonassoc。它们也是定义文法中使用的终结符，定义形式与%token类似，但是他们定义的终结符具有某种优先级和结合性，%left表示左结合，%right表示右结合，%nonassoc表示不可结合（即它定义的终结符不能连续出现：例如<，如果文法中不允许出现形如a<b<c的句子，则<就是不可结合的）。而优先级关系则是以他们定义出现的顺序决定的，先定义的优先级低，最后定义的优先级最高，同时定义的优先级相同。
    



